@TEAM ENVI^ EnviMode=1| ENVI^ ForceLocal=1
CALL $--ret:&DPIA user32.dll,SetProcessDPIAware //设置dpi感知模式
CALL $--ret:&hdc user32.dll,GetDC,#0  //获取显示器0（一般是主显示器）的句柄
CALL $--ret:&LOGPIXELSX Gdi32.dll,GetDeviceCaps,#%hdc%,#88     //获取显示器横向DPI，LOGPIXELSX为#88，纵向LOGPIXELSY为#90,两个值是一样的。
ENVI &&n=0
ENVI &&m=0
ENVI &&acd=
ENVI &&abcd=
ENVI &&Pagetag=
CALL @WIN1
_SUB 获取列表文件
    BROW txtfile,, 请选择列表文件,txt
    FIND $%txtFILE%=,!ENVI @列表路径=%txtFILE%
_END
_SUB 获取源文件
    BROW MntDir1,*%源路径%,请选择源文件目录
    FIND $%Mntdir1%=,!ENVI @源路径=%Mntdir1%
_END
_SUB 保存路径
    BROW MntDir2,*%目标路径%,请选择保存目录
    FIND $%Mntdir2%=,!ENVI @目标路径=%Mntdir2%
_END
_SUB 获取文件目录
    BROW MntDir3,*%文件目录%,请选择文件目录
    FIND $%Mntdir3%=,!ENVI @文件目录=%Mntdir3%
_END
_SUB 获取wim路径
    BROW wimfile,,请选择wim文件,wim文件|*.wim|esd文件|*.esd|所有文件|*.*
    FIND $%wimFILE%=,!ENVI @wim路径=%wimFILE%
_END
_SUB 获取wim列表文件
    BROW txtfile,,请选择列表文件,txt
    FIND $%txtFILE%=,!ENVI @wim列表路径=%txtFILE%
_END
_SUB wim保存路径
    BROW MntDir4,*%源路径%,请选择保存目录
    FIND $%Mntdir4%=,!ENVI @wim保存路径=%Mntdir4%
_END
_SUB 注册表单元文件
    BROW file,,注册表单元,
    FIND $%file%=,!ENVI @注册表单元路径=%file%
_END
_SUB 注册表文件
    BROW regfile,,注册表文件,注册表|*.reg,,,
    FIND $%regfile%=,!ENVI @注册表文件路径=%regfile%
_END
_SUB WIN1,W684H500,文件提取,KILL  *%&__PID%,,,,-scale:%&LOGPIXELSX%
    ENVI @WIN1.Font=12:宋体
    TABS TABS1,L0T0W684H435,Page1::复制:根据文件列表复制;Page2::文件列表:获取文件列表;Page3::wimlib:直接提取wim文件;Page4::离线注册表:挂载卸载注册表单元
    LABE -left -vcenter lblxSize,L47T440W105H20,耗时：0秒,,0xB1
    TIME Timer1,0,CALL TimeRefresh
    DATE &iTimeStart uptime
_END
_SUB Page1,,,,,#,,-scale:%&LOGPIXELSX%
    //ENVI @Page1.Font=12:宋体
    LABE -ncmd -vcenter 列表标签,L33T13W82H25,列表文件:,,,12:宋体
    ITEM -font:12:宋体 列表按钮,L570T13W60H25,浏览,TEAM CALL 获取列表文件|THREAD* CALL 计数,
    EDIT. -vcenter 列表路径,L128T13W430H25,,,0x100,,10:宋体
    LABE -ncmd -vcenter 来源标签,L33T56W82H25,来源路径:,,,12:宋体
    ITEM -font:12:宋体 来源按钮,L570T55W60H25,浏览,CALL 获取源文件,
    EDIT. -vcenter 源路径,L128T55W430H25,,,0x100,,10:宋体    
    LABE -ncmd -vcenter 目标标签,L35T96W82H25,保存路径:,,,12:宋体
    ITEM -font:12:宋体 目标按钮,L570T97W60H25,浏览,CALL 保存路径,
    EDIT. -vcenter 目标路径,L127T97W430H25,,,0x100,,10:宋体
    MEMO. MEMO1,L33T180W613H175,,,0x48
    PBAR PBAR1,L33T139W595H25,PBAR1
    ITEM -font:12:宋体 ITEM1,L298T359W81H40,执行,THREAD* TEAM CALL Start|CALL TimeRefresh|MESS= 完成,
    ITEM -font:12:宋体 ITEM2,L298T359W81H40,暂停,CALL Stop,,-16
    RADI RADI1,L520T359W102H20,按列表复制,CALL RadiTrans 1,1,1
    ENVI @RADI1.Font=12:宋体
    RADI RADI2,L520T378W101H20,按列表删除,CALL RadiTrans 2,,1
    ENVI @RADI2.Font=12:宋体
    CHEK CHEK1,,,,-2
    //mess %CHEK1.Check%
    CHEK CHEK2,L48T359W95H20,输出日志,,
    ENVI @CHEK2.Font=12:宋体
    //ITEM Item2,L200T150W50H30,执行,THREAD* TEAM CALL TimeStart|CALL TimeRefresh|MESS. 完成
_END

_SUB Start
    TEAM FORX %列表路径%,Mytxt,1,FEXT fEx=%Mytxt%|FEXT fD=%源路径%
    IFEX [ '%fEx%'='txt' & '%fD%'='' & '%源路径%'<>'' ],
    {
        ENVI @ITEM1.Enable=0
        ENVI @ITEM1.Visible=0
        ENVI @ITEM2.Enable=1
        ENVI @ITEM2.Visible=1
        ENVI @CHEK2.Enable=0
        ENVI @RADI1.Enable=0
        ENVI @RADI2.Enable=0
        ENVI &Pagetag=复制
        CALL TimeStart     
    }!
    {
        MESS= 请选择正确的列表文件和目录
        EXIT ToWin
    }
_END
_SUB Stop
    ENVI @CHEK1.Check=1 
_END
_SUB 计数
    READ "%列表路径%",*r,&&all
    ENVI &acd=%&&all%    
    //SET &n=0
    FORX *NL &all,&&content,
    {
        FIND *&content=,EXIT -    
        ^CALC &n=%&n%+1
        //MESS %n%
    }
_END

_SUB Main
    //READ "%列表路径%",*r,&&all    
    //SET &m=0 
    //mess %acd% 
    DATE &iTimeStart uptime
    FORX *NL &acd,&&content,
    {
        FIND *&content=,EXIT - 
        //MESS %&m%       
        MSTR contentnew=-1,1,%&content%
        IFEX |%contentnew%=\,
        {*
            STRL number1=%&content%
            CALC ta=%number1%-1
            MSTR &&content=1,%ta%,%&content%
        }
        TEAM FIND $%CHEK1.Check%=1,MESS-icon5 点击确定继续 @暂停中#OK|ENVI @CHEK1.Check=0
        IFEX |%RADI1.Check%=1,
        {*
            IFEX |%目标路径%=,FDIR &&Dir=%&CurDir%\%&content%!FDIR &&Dir=%目标路径%\%&content%
            //MESS %&Dir%
            //EXIT ToWin    
            FIND ! "%&Dir%",PATH %&Dir%
            TEAM FILE "%源路径%\%&content%"=>"%&Dir%"|SET &abcd=%&ERROR%
            IFEX [ $%&abcd%=0 | $%&abcd%=2 ],
            {
                //MESS %&abcd%
                FIND $%&abcd%=0,
                {
                    TEAM SET  &V=%MEMO1%|WRIT *v  &V,$+0,%&content%复制成功！|ENVI @MEMO1=%&V%
                }!
                {
                    TEAM SET  &V=%MEMO1%|WRIT *v  &V,$+0,%&content%文件不存在！|ENVI @MEMO1=%&V%   
                } 
            }!
            {
                TEAM SET  &V=%MEMO1%|WRIT *v  &V,$+0,%&content%复制失败！|ENVI @MEMO1=%&V%
            }  
            //MESS %&abcd%
        }!
        {*
            TEAM FILE "%源路径%\%&content%"|SET &abcd=%&ERROR%            
            IFEX [ $%&abcd%=0 | $%&abcd%=2 ],
            {
                //MESS %&abcd%
                FIND $%&abcd%=0,
                {
                    TEAM SET  &V=%MEMO1%|WRIT *v  &V,$+0,%&content%删除成功！|ENVI @MEMO1=%&V%
                }!
                {
                    TEAM SET  &V=%MEMO1%|WRIT *v  &V,$+0,%&content%文件不存在！|ENVI @MEMO1=%&V%   
                } 
            }!
            {
                TEAM SET  &V=%MEMO1%|WRIT *v  &V,$+0,%&content%删除失败！|ENVI @MEMO1=%&V%
            }  
            //MESS %&abcd%
        }
        ^CALC &m=%&m%+1
        //mess. %m%
        ^CALC &vvv=%&m%/%&n%*100##1
        //CALC &P=floor(%&vvv%)##2
        ENVI &&F=%&m%/%n%
        ENVI &PA=%&vvv%%
        ENVI @PBAR1=%&vvv%;#0X000000:已完成%&PA%：%&F%  
    }
    FIND $%CHEK2.Check%=1,WRIT *c %CurDir%\LOG.txt,$+0,%MEMO1% 
    ENVI @ITEM2=完成
    ENVI @ITEM2.Enable=0
_END

_SUB TimeStart
    //mess %Pagetag%
    IFEX |%Pagetag%=复制,
    {
        DATE &iTimeStart uptime
        ENVI @Timer1=50
        CALL Main
        ENVI @Timer1=0
    }
    IFEX '%Pagetag%'='列表',
    {
         IFEX $%RADIdll1.Check%=1,
        {
            ENVI @Image1.Visible=1
            DATE &iTimeStart uptime
            ENVI @Timer1=50
            CALL FileListDir
            ENVI @Timer1=0
        }!
        {
            IFEX '%程序名称%'='',
            {
                MESS. 请输入程序名
                EXIT ToWin
            }!
            {
                ENVI @Image1.Visible=1
                DATE &iTimeStart uptime
                ENVI @Timer1=50
                CALL FileListDir
                ENVI @Timer1=0
            }
        }     
    }
    IFEX |%Pagetag%=wimlib,
    {
        DATE &iTimeStart uptime
        ENVI @Timer1=50
        CALL wim提取
        ENVI @Timer1=0
    }
    IFEX |%Pagetag%=reg,
    {
        DATE &iTimeStart uptime
        ENVI @Timer1=50
        CALL regedit
        ENVI @Timer1=0
    }    
_END
_SUB TimeRefresh
    TEAM DATE &iTimeCur uptime|CALC iTimes=(%&iTimeCur%-%&iTimeStart%)/1000##2
    IFEX $%iTimes%>=60,
    {*
        ^CALC #分=%iTimes%/60
        ^CALC #秒=%iTimes%-%分%*60
        ENVI @lblxSize=耗时：%分%分%秒%秒
    }!
    {*
        ENVI @lblxSize=耗时：%iTimes%秒
    }
_END
_SUB RadiTrans
    //mess. %1
    IFEX $%1=2,
    {
        ENVI @目标标签.Visible=0
        ENVI @目标按钮.Visible=0
        ENVI @目标路径.Visible=0
    }
    IFEX $%1=1,
    {
        ENVI @目标标签.Visible=1
        ENVI @目标按钮.Visible=1
        ENVI @目标路径.Visible=1
    }
_END
_SUB Page2,W4004H284,,,,#,,-scale:%&LOGPIXELSX%
    ENVI @Page2.Font=12:宋体
    LABE -ncmd -vcenter List标签,L50T55W80H25,获取目录:,,,12:宋体
    EDIT. -vcenter 文件目录,L128T55W430H25,,,0x100,,10:宋体
    LABE -ncmd -vcenter Listdll,L50T55W70H25,程序名:,,,12:宋体
    ENVI @Listdll.Visible=0
    EDIT. -vcenter 程序名称,L128T55W430H25,,,0x100,,10:宋体
    ENVI @程序名称.Visible=0
    IMAG Image1,L128T100W430H150,#1|GIF
    ENVI @Image1.Visible=0
    ITEM -font:12:宋体 List按钮,L570T55W60H25,浏览,CALL 获取文件目录,
    ITEM -font:12:宋体 文件按钮,L298T300W81H40,执行,THREAD* TEAM ENVI &Pagetag=列表|CALL TimeStart|CALL TimeRefresh|ENVI @Image1.Visible=0|MESS. 完成,
    RADI RADIdll1,L520T299W132H20,获取文件列表,CALL RadiTrans2 1,1,1
    RADI RADIdll2,L520T318W131H20,获取文件依赖,CALL RadiTrans2 2,,1
    ENVI @RADI1.Font=12:宋体
    TIME Timer1,0,CALL TimeRefresh
    DATE &iTimeStart uptime
_END
_SUB RadiTrans2
    IFEX $%1=1,
    {
        ENVI @Listdll.Visible=0
        ENVI @程序名称.Visible=0
        ENVI @List按钮.Visible=1
        ENVI @List标签.Visible=1
        ENVI @文件目录.Visible=1
    }
    IFEX $%1=2,
    {
        ENVI @Listdll.Visible=1
        ENVI @文件目录.Visible=0
        ENVI @List按钮.Visible=0
        ENVI @程序名称.Visible=1
        ENVI @List标签.Visible=0
    }
_END
_SUB FileListDir
    IFEX |%RADIdll1.Check%=1,
    {
        DATE &iTimeStart uptime
        DIR &Filelist /s/a:-d/O:N "%文件目录%"
        IFEX |%文件目录%<>,
        {
            SED -ts &Filelist=0,%文件目录%\,,%&Filelist%
            //MESS. %&文件的列表%
            WRIT *c %CurDir%\Filelist.txt,$+0,%&Filelist%
        }!
        {
            WRIT *c %CurDir%\Filelist.txt,$+0,%&Filelist%
        }
    }!
    {
            //MESS. %程序名称%
            ENVI @文件按钮.Enable=0
            EXEC* -err+ -code:ANSI -pid:varname --exe:*#101 &OutVar2=!Listdlls64.exe %程序名称%
            //READ "F:\PECMD程序\文件复制\新建文件夹\DLLS.txt",*r,&all
            LPOS* * ,*&定位行数=Path,,&OutVar2
            //MESS. %&定位行数%
            READ ,-1,line,%&OutVar2%
            CALC length=%line%-%Path%
            READ ,(%&定位行数%+1)+%length%,&contant,%&OutVar2%
            FORX  *NL &contant,&linecontent,
            {*
                FIND *&linecontent=,EXIT -
                MSTR &dll=<3->%&linecontent%
                WRIT *c *v &dlls,$+0,%&dll%
                //MESS. %&dlls%
            }
            //MESS. %&dlls%
            WRIT *c %CurDir%\DLLS.txt,$+0,%&dlls%
    }  
_END
_SUB Page3,W681H498,,,,,,-scale:%&LOGPIXELSX%
    ENVI @Page3.Font=12:宋体
    LABE -ncmd -vcenter wimlib提取标签,L37T49W80H25,wim路 径:,,
    EDIT. -vcenter wim路径,L122T49W430H25,,,0x100,,10:宋体
    ITEM -font:12:宋体 wim按钮,L557T49W60H25,浏览,THREAD* TEAM CALL 获取wim路径|CALL wiminfo,
    LABE -ncmd -vcenter wimlib保存标签,L37T117W80H25,保存路径:,,
    EDIT. -vcenter wim保存路径,L121T117W430H25,,,0x100,,10:宋体
    ITEM -font:12:宋体 wim保存按钮,L556T117W60H25,浏览,CALL wim保存路径,
    MEMO. MEMO2,L37T185W600H175,,,0x48,,10:宋体
    ITEM -font:12:宋体 wim提取按钮,L292T367W81H40,执行,THREAD* TEAM ENVI &Pagetag=wimlib|CALL TimeStart|CALL TimeRefresh|MESS= 完成,
    LIST -vcenter wimList,L121T82W496H25,,ENVI &&wimvolume=%wimList.isel%,
    //ENVI @wimList.Font=10:宋体
    LABE -ncmd -vcenter 卷标签,L37T83W80H25,请选择卷:,,
    LABE -ncmd -vcenter wimlib列表文件,L37T15W80H25,列表文件:,,
    EDIT. -vcenter wim列表路径,L123T15W430H25,,,0x100,,10:宋体
    ITEM -font:12:宋体 wim列表按钮,L557T15W60H25,浏览,CALL 获取wim列表文件,
    PBAR PBAR2,L37T150W578H22,PBAR1
_END
_SUB Page4,W603H364,,,,#,,-scale:%&LOGPIXELSX%
    ENVI @Page4.Font=12:宋体
    LABE -ncmd -vcenter 注册表,L37T49W90H25,挂载  名称：,,
    EDIT. -vcenter 挂载名称,L132T49W430H25,,,,,10:宋体
    LABE -ncmd -vcenter 注册表单元路径标签,L37T89W90H25,单元  路径：,,
    EDIT. -vcenter 注册表单元路径,L132T89W430H25,,,3,,10:宋体
    ENVI @注册表单元路径.MSG=0x0203:CALL 注册表单元文件
    //ITEM -font:12:宋体 注册表单元按钮,L567T89W60H25,浏览,CALL 注册表单元文件,
    LABE -ncmd -vcenter 注册表文件路径标签,L37T129W90H25,注册表路径：,,
    MEMO. 注册表文件路径,L132T129W447H125,,,0x218,,10:宋体
    ENVI @注册表文件路径.MSG=0x0203:CALL 注册表文件
    //ITEM -font:12:宋体 注册表文件按钮,L567T129W60H25,浏览,CALL 注册表文件,,16
    RADI -center regRADI1,L137T299W60H30,挂载,CALL regraditrans,1,2
    RADI -center regRADI2,L299T299W100H30,挂载并导入,CALL regraditrans,,2
    RADI -center regRADI3,L501T299W60H30,卸载,CALL regraditrans,,2
    ITEM regITEM,L303T328W84H40,开始,THREAD* TEAM ENVI &Pagetag=reg|CALL TimeStart|CALL TimeRefresh,
    LABE -ncmd -vcenter -center 状态,L215T256W232H36,,,0x0000FF
    ENVI @状态.Visible=0
    ENVI @注册表文件路径标签.Visible=0
_END
_SUB regedit
    TEAM ENVI @状态.Visible=1|ENVI @状态=正在执行
    //mess %注册表文件路径%
    //WAIT 2000
    //MESS %regRADI1.Check% %regRADI2.Check%
    IFEX [ '%regRADI1.Check%'='1' | '%regRADI2.Check%'='1' & '%注册表单元路径%'<>'' ],
    {
        //mess HIVE %挂载名称%,%注册表单元路径%
        IFEX '%注册表单元路径%'<>'' , 
        {   	
			HIVE %挂载名称%,%注册表单元路径%
			IFEX [ '%regRADI2.Check%'='1' ],
			{
				ENVI &&DIR=%TEMP%\%random%
				//MESS %&&DIR%
				FORX *NL &注册表文件路径,&Path,
				{*
					//MESS %Path%
					FIND *&Path=,EXIT - 
					CALL reganalyse ret
					//MESS %ret%
					//MESS "%&DIR%\%&fNam%"
					EXEC regedit /s "%ret%"
				}
			}
	}!
        {
            MESS 请先挂载配置单元
        }
    }!
    {
        HIVE %挂载名称%
    }
    ENVI @状态=完成
_END
_SUB reganalyse
    READ "%&Path%",*r,&&regcontent
    //MESS 1 %&&regcontent%
    LPOS* * ,*&定位行数=[,,&&regcontent
    //MESS %&定位行数%
    MSTR &第一行=<1>%&定位行数%
    //MESS %&第一行%
    READ  -,%&第一行%,&挂载名称内容,%&&regcontent%
    MSTR -delims:\ &name=<2>%&挂载名称内容%
    //MESS 2 %&name%
    IFEX '%&name%'<>'%挂载名称%',
    {*
        SED -ts -ni &&regcontent=0,%&name%,%挂载名称%,%&&regcontent%
        //MESS 3 %&&regcontent% 
    }
    FNAM -ext &&fNam=%&Path%
    PATH %&DIR%
    set-ret %1=%&DIR%\%fNam%
    WRIT *c %&DIR%\%fNam%,$+0,%&&regcontent%
    //MESS 4 %&&regcontent%
_END
_SUB regraditrans
    IFEX '%regRADI3.Check%'='1',
    {
        ENVI @注册表文件路径标签.visible=0
        ENVI @注册表文件路径.visible=0
        ENVI @注册表文件按钮.visible=0
        ENVI @注册表单元路径标签.visible=0
        ENVI @注册表单元路径.visible=0
        ENVI @注册表单元按钮.visible=0
    }!
    {
        IFEX '%regRADI1.Check%'='1',
        {
            ENVI @注册表文件路径标签.visible=0
            ENVI @注册表文件路径.visible=0
            ENVI @注册表文件按钮.visible=0
            ENVI @注册表单元路径标签.visible=1
            ENVI @注册表单元路径.visible=1
            ENVI @注册表单元按钮.visible=1
        }!
        {
            ENVI @注册表文件路径标签.visible=1
            ENVI @注册表文件路径.visible=1
            ENVI @注册表文件按钮.visible=1
            ENVI @注册表单元路径标签.visible=1
            ENVI @注册表单元路径.visible=1
            ENVI @注册表单元按钮.visible=1
        }
    }
_END
_SUB wim提取   
    EXEC* -err+ -code:ANSI -cmd:OnTransNL1 -pid:varname --exe:*cab:#100:wimlib-imagex.exe &OutVar1=!wimlib-imagex.exe extract "%wim路径%" %&wimvolume% @"%wim列表路径%" --dest-dir="%wim保存路径%" --nullglob    
_END
_SUB OnTransNL1 *
    SED &&提取进度=0,\n,\r\n,%&OutVar1%
    ENVI @MEMO2=%&提取进度%
    LPOS* * ,*&定位行数1=Creating files,,&提取进度
    LPOS* * ,*&定位行数2=Extracting file data,,&提取进度
    LPOS* * ,*&定位行数3=Applying metadata to files,,&提取进度
    LPOS* * ,*&定位行数4=Done extracting files,,&提取进度
    IFEX [ $%&定位行数2%=0 & $%&定位行数1%<>0 ],
    {*
        MSTR &最后一行=<-1>%&定位行数1%
        READ -,%&最后一行%,&进度内容,%&提取进度%
        MSTR -delims:( &进度=<2>%&进度内容%
        MSTR -delims:) &new进度=<1>%&进度%
        STRL i=%&new进度%
        CALC l=%i%-1
        MSTR 百分数=1,%l%,%&new进度%
        ENVI @PBAR2=%&百分数%;#0X000000:创建文件：%百分数%%
    }
    IFEX [ $%&定位行数3%=0 & $%&定位行数2%<>0 ],
    {*
        MSTR &最后一行=<-1>%&定位行数2%
        READ -,%&最后一行%,&进度内容,%&提取进度%
        MSTR -delims:( &进度=<2>%&进度内容%
        MSTR -delims:) &new进度=<1>%&进度%
        STRL i=%&new进度%
        CALC l=%i%-1
        MSTR 百分数=1,%l%,%&new进度%
        ENVI @PBAR2=%&百分数%;#0X000000:提取文件：%百分数%%
    }        
    IFEX [ $%&定位行数4%=0 & $%&定位行数3%<>0 ],
    {*
        MSTR &最后一行=<-1>%&定位行数3%
        READ  -,%&最后一行%,&进度内容,%&提取进度%
        MSTR -delims:( &进度=<2>%&进度内容%
        MSTR -delims:) &new进度=<1>%&进度%
        STRL i=%&new进度%
        CALC l=%i%-1
        MSTR 百分数=1,%l%,%&new进度%
        ENVI @PBAR2=%&百分数%;#0X000000:应用元数据：%百分数%%
    }        
    IFEX $%&定位行数4%<>0,
    {*
        ENVI @PBAR2=100;#0X000000:提取文件完成
    }
_END
_SUB wiminfo
    EXEC* -err+ wiminfo2=!Dism.exe /Get-ImageInfo /ImageFile:%wim路径%
    SED &&winfo=0,\n,\r\n,%wiminfo2%
    LPOS* * ,*&定位行数=名称:,,&winfo
    //mess. %定位行数%
    IFEX $%定位行数%<>0,
    {*
        FORX *NL:  &定位行数,&&infocontents,
        {*
            READ  -,%&infocontents%,&内容,%&winfo%
            MSTR -delims:: &&wiminfocontent=<2>%内容%
            //mess. %&wiminfocontent%
            ENVI @wimList.ADDSEL=%&wiminfocontent%
            ENVI @wimList.isel=1
        }
    }
_END
